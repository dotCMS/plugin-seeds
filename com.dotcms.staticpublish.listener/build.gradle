plugins {
    id 'biz.aQute.bnd.builder' version '3.3.0'
}

sourceCompatibility = '1.8'
version = '0.2'

repositories {
    maven { url "http://repo.dotcms.com/artifactory/libs-release" }
}

configurations {
    pluginLibs
}

dependencies {
    compile('com.dotcms:dotcms:22.03.1') { transitive = true }

    compileOnly('com.hierynomus:sshj:0.23.0'){
        exclude group: 'org.bouncycastle' //Why we exlude? See README's important notes.
    }
    compileOnly('org.slf4j:slf4j-simple:1.7.7')
}

import java.util.jar.*

/////////////////////////
//Plugin jar
/////////////////////////
jar {
    manifest {
        attributes (
                'Bundle-Vendor': 'dotCMS',
                'Bundle-Description': 'dotCMS - OSGI Static Event Listener example',
                'Bundle-DocURL': 'https://dotcms.com/',
                'Bundle-Activator': 'com.dotcms.staticpublish.Activator',
                'Bundle-ClassPath': '''
                    .,
                    libs/eddsa-0.2.0.jar,
                    libs/jzlib-1.1.3.jar,
                    libs/slf4j-api-1.7.7.jar,
                    libs/slf4j-simple-1.7.7.jar,
                    libs/sshj-0.23.0.jar
                ''',
                'Export-Package':'''
                    org.bouncycastle.jce.provider.*,
                    org.bouncycastle.jcajce.provider.asymmetric.rsa.DigestSignatureSpi.*,
                    org.bouncycastle.jcajce.provider.digest.*,
                    org.bouncycastle.jcajce.provider.asymmetric.ec.KeyAgreementSpi.*,
                    org.bouncycastle.jcajce.provider.asymmetric.*
                ''',
                'Import-Package': '*;version=0'
        )
    }
}

task copyToLib(type: Copy) {
    into "src/main/resources/libs"
    from configurations.compileOnly
}

jar.dependsOn copyToLib
jar.finalizedBy 'fragmentJar'

/////////////////////////
//Fragment jar
/////////////////////////

ext {
    bundleName = "OSGI Static Event Listener fragment"
    bundleDescription = "dotCMS - OSGI Static Event Listener fragment"
    fragmentHost = "system.bundle; extension:=framework"
    bundleSymbolicName = "" //Auto generated based on the plugin jar
    bundleVersion = "" //Auto generated based on the plugin jar
    importPackage = "" //Auto generated based on the plugin jar
    bundleManifestVersion = "" //Auto generated based on the plugin jar
    bundleDocURL = "" //Auto generated based on the plugin jar
    bundleVendor = "" //Auto generated based on the plugin jar
}
/**
 * The import generates versions like this: version="[1.8,2)"
 * That format does not work for the export, so we need to replace it
 * to: version=0
 */
ext.fixVersionNumber = {importValue ->
    return importValue.replaceAll("\"\\[[0-9.,]+\\)\"", "0")
}

/**
 * Reads the Manifest file of the just created plugin jar in order to get the required info
 * to automatically create the fragment jar.
 */
task readManifesttAttributes {
    doFirst {
        File file = configurations.baseline.singleFile
        JarFile jar = new JarFile(file)
        Attributes manifest = jar.getManifest().getMainAttributes()
        bundleSymbolicName = "${manifest.getValue('Bundle-SymbolicName')}"
        bundleVersion = "${manifest.getValue('Bundle-Version')}"
        importPackage = "${manifest.getValue('Import-Package')}"
        bundleManifestVersion = "${manifest.getValue('Bundle-ManifestVersion')}"
        bundleDocURL = "${manifest.getValue('Bundle-DocURL')}"
        bundleVendor = "${manifest.getValue('Bundle-Vendor')}"
    }
}
task fragmentJar(type: Jar) {

    doFirst {
        //Setting the fragment jar name
        baseName = project.name
        archiveName = "${baseName}.fragment-${version}.jar"
        importPackage = fixVersionNumber(importPackage)

        manifest {
            attributes (
                    'Bundle-Name': "${bundleName}",
                    'Bundle-Description': "${bundleDescription}",
                    'Bundle-Vendor': "${bundleVendor}",
                    'Bundle-Version': "${version}",
                    'Bundle-SymbolicName': "${baseName}.fragment",
                    'Bundle-ManifestVersion': "${bundleManifestVersion}",
                    'Bundle-DocURL': "${bundleDocURL}",
                    'Fragment-Host': "${fragmentHost}",
                    'Export-Package': "${importPackage}"
            )
        }
    }
}
fragmentJar.dependsOn 'readManifesttAttributes'

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}
